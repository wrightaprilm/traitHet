#####################################
# GTR+G nucleotide substitution model #
#####################################
# Initialize the parameters of substitution matrix #
# Both the stationary frequencies and exchangeability rates #
# are drawn from uniform Dirichlet distributions #

# First the stationary frequencies #
sf_hp <- v(1.0,1.0,1.0,1.0)
sf ~ dnDirichlet(sf_hp)

# Then the exchangeability rates #
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)

moves.append(mvSimplexElementScale(er, alpha=10.0, weight=5.0))
moves.append(mvSimplexElementScale(sf, alpha=10.0, weight=5.0))

# Create the matrix #
Q_mol := fnGTR(er,sf)

# Create a discretized gamma distribution of rate categories  #
# the shape parameter is drawn from an exponential prior #
alpha_mol ~ dnExponential( 1.0 )

moves.append(mvScale(alpha_mol, lambda=0.01, weight=1.0))
moves.append(mvScale(alpha_mol, lambda=0.1,  weight=1.0))
moves.append(mvScale(alpha_mol, lambda=1,    weight=1.0))

rates_mol := fnDiscretizeGamma( alpha_mol, alpha_mol, 4 )


### Create the substitution model and clamp with our observed DNA data ###
phySeq ~ dnPhyloCTMC(tree=fbd_tree, Q=Q_mol, siteRates=rates_mol, branchRates = branch_rates_mean, type="DNA")
phySeq.clamp(data)
